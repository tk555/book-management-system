buildscript {
	dependencies {
		classpath 'org.postgresql:postgresql:42.7.5'
		classpath 'org.flywaydb:flyway-database-postgresql:12.0.0'
	}
}

plugins {
	id 'org.jetbrains.kotlin.jvm' version '2.2.21'
	id 'org.jetbrains.kotlin.plugin.spring' version '2.2.21'
	id 'org.springframework.boot' version '4.0.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.openapi.generator' version '7.10.0'
	id 'org.jooq.jooq-codegen-gradle' version '3.19.29'
	id 'org.flywaydb.flyway' version '12.0.0'
	id 'com.diffplug.spotless' version '7.0.2'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-flyway'
	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'org.flywaydb:flyway-database-postgresql'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'tools.jackson.module:jackson-module-kotlin'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'
	implementation 'com.fasterxml.uuid:java-uuid-generator:5.1.0'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	runtimeOnly 'org.postgresql:postgresql'
	jooqCodegen 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-flyway-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-jooq-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.testcontainers:junit-jupiter:1.20.4'
	testImplementation 'org.testcontainers:postgresql:1.20.4'
}

kotlin {
	compilerOptions {
		freeCompilerArgs.addAll '-Xjsr305=strict', '-Xannotation-default-target=param-property'
	}
}

tasks.named('test') {
	useJUnitPlatform()

	testLogging {
		events 'passed', 'skipped', 'failed'
		exceptionFormat 'full'
		showStandardStreams = false
		showCauses = true
		showStackTraces = true
	}
}

openApiGenerate {
	generatorName = 'kotlin-spring'
	inputSpec = "$projectDir/src/main/resources/openapi.yaml"
	outputDir = "$buildDir/generated/openapi"
	apiPackage = 'com.example.demo.api'
	modelPackage = 'com.example.demo.model'
	configOptions = [
		interfaceOnly: 'true',
		useSpringBoot3: 'true',
		useTags: 'true'
	]
}

jooq {
	executions {
		main  {
			configuration {
				logging = 'DEBUG'

				jdbc {
					driver = 'org.postgresql.Driver'
					url = 'jdbc:postgresql://localhost:5432/mydatabase'
					user = 'myuser'
					password = 'secret'
				}

				generator {
					name = 'org.jooq.codegen.KotlinGenerator'

					database {
						name = 'org.jooq.meta.postgres.PostgresDatabase'
						inputSchema = 'public'
						includes = '.*'
						excludes = 'flyway_schema_history'
					}

					target {
						packageName = 'com.example.demo.jooq'
						directory = 'build/generated/jooq'
					}

					generate {
						deprecated = false
						records = true
						pojos = true
						immutablePojos = true
						fluentSetters = true
						kotlinNotNullPojoAttributes = true
						kotlinNotNullRecordAttributes = true
					}
				}
			}
		}
	}
}

sourceSets {
	main {
		kotlin {
			srcDir "$buildDir/generated/openapi/src/main/kotlin"
			srcDir "$buildDir/generated/jooq"
		}
	}
}

flyway {
	url = 'jdbc:postgresql://localhost:5432/mydatabase'
	user = 'myuser'
	password = 'secret'
	locations = ["filesystem:${projectDir}/src/main/resources/db/migration"]
}

tasks.named('jooqCodegen') {
	dependsOn tasks.flywayMigrate
}

tasks.named('compileKotlin') {
	dependsOn tasks.openApiGenerate
}

spotless {
	kotlin {
		target 'src/**/*.kt'
		ktlint()
	}
}
